; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=BestCAD 2026
AppVerName=BestCAD 2026 64bit
AppPublisher=CADmost
AppPublisherURL=http://www.cadmost.com.pl
AppSupportURL=http://www.bestcad.com.pl
AppUpdatesURL=http://www.bestcad.com.pl
DefaultDirName={pf}\BestCAD 2026
DefaultGroupName=BestCAD 2026
DisableProgramGroupPage=yes
AllowNoIcons=false
LicenseFile=d:\3_Instalki\BestInstSetup\$BestLinki\251a\OutPutAC\Licencja.txt
InfoBeforeFile=d:\3_Instalki\BestInstSetup\$BestLinki\251a\OutPutAC\Readme_64bit.txt
InfoAfterFile=d:\3_Instalki\BestInstSetup\$BestLinki\251a\OutPutAC\Zmiany.txt
OutputDir=d:\3_Instalki\BestInstSetup\$BestLinki\251a\OutPutAC
SourceDir=d:\3_Instalki\BestInstSetup\$BestLinki\251a
OutputBaseFilename=BestCAD2026_64bit
AlwaysRestart=false
PrivilegesRequired=admin
MinVersion=6.1
DisableStartupPrompt=no
RestartIfNeededByRun=true
UninstallDisplayIcon={app}\Best2026.ico
UninstallFilesDir={app}
DirExistsWarning=no
VersionInfoVersion=251
VersionInfoCompany=CADmost
VersionInfoDescription=BestCAD 2026 dla AutoCAD 2024, 2025, 2026 64bit
ShowLanguageDialog=yes
AppVersion=251a
WizardImageFile=d:\3_Instalki\BestInstSetup\$BestLinki\251a\BeStCAD\ams_instaler1.bmp
WizardSmallImageFile=d:\3_Instalki\BestInstSetup\$BestLinki\251a\BeStCAD\ams_instaler2.bmp
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
;DisableDirPage=true

[Files]
;-----------------------------------------------------------
;; common links for FullAC and Lt
;-----------------------------------------------------------
;; links from BestCAD
Source: BestCAD\*.*; DestDir: {app}
;; links from BestCAD\AplFiles
Source: BestCAD\AplFiles\*.*; DestDir: {app}\AplFiles
Source: BestCAD\AplFiles\Param\Typost\*.*; DestDir: {app}\AplFiles\Param\Typost
Source: BestCAD\AplFiles\Param\Zelbet\*.*; DestDir: {app}\AplFiles\Param\Zelbet
Source: BestCAD\AplFiles\Param\Stal\*.*; DestDir: {app}\AplFiles\Param\Stal
Source: BestCAD\AplFiles\Przycz\Szablony\*.*; DestDir: {app}\AplFiles\Przycz\Szablony
Source: BestCAD\AplFiles\Sprez\*.*; DestDir: {app}\AplFiles\Sprez
Source: BestCAD\AplFiles\BazaOp\*.*; DestDir: {app}\AplFiles\BazaOp
Source: BestCAD\AplFiles\Images\*.*; DestDir: {app}\AplFiles\Images
Source: BestCAD\AplFiles\Cuix\*.*; DestDir: {app}\AplFiles; Check: IsCUIX
;Source: BestCAD\AplFiles\Mns\*.*; DestDir: {app}\AplFiles
Source: BestCAD\AplFiles\Mns\*.*; DestDir: {app}\AplFiles; Check: IsMns
;; links from BestCAD\Pomoc
Source: BestCAD\Pomoc\01-Modu³Ogólny\*.*; DestDir: {app}\Pomoc\01-Modu³Ogólny
Source: BestCAD\Pomoc\02-Modu³Stal\*.*; DestDir: {app}\Pomoc\02-Modu³Stal
Source: BestCAD\Pomoc\03-Modu³¯elbet\*.*; DestDir: {app}\Pomoc\03-Modu³¯elbet
Source: BestCAD\Pomoc\04-Modu³Mosty\*.*; DestDir: {app}\Pomoc\04-Modu³Mosty
Source: BestCAD\Pomoc\05-Modu³Sprê¿enie\*.*; DestDir: {app}\Pomoc\05-Modu³Sprê¿enie
Source: BestCAD\Pomoc\06-Modu³In¿ynier\*.*; DestDir: {app}\Pomoc\06-Modu³In¿ynier
Source: BestCAD\Pomoc\07-Modu³Drogi\*.*; DestDir: {app}\Pomoc\07-Modu³Drogi
Source: BestCAD\Pomoc\08-Modu³Sprê¿enie2\*.*; DestDir: {app}\Pomoc\08-Modu³Sprê¿enie2
Source: BestCAD\Pomoc\09-Modu³Geotechnika\*.*; DestDir: {app}\Pomoc\09-Modu³Geotechnika
;; links from BestCAD\Lib
Source: BestCAD\Lib\Most\Przycz\*.*; DestDir: {app}\Lib\Most\Przycz
Source: BestCAD\Lib\Most\TraNiw\*.*; DestDir: {app}\Lib\Most\TraNiw
Source: BestCAD\Lib\SPR\*.*; DestDir: {app}\Lib\SPR
Source: BestCAD\Lib\Symb\*.*; DestDir: {app}\Lib\Symb
Source: BestCAD\Lib\Tab\*.*; DestDir: {app}\Lib\Tab
Source: BestCAD\Lib\Zelb\*.*; DestDir: {app}\Lib\Zelb
Source: BestCAD\Lib\Zn\*.*; DestDir: {app}\Lib\Zn
Source: BestCAD\Lib\Stal\Eng\*.*; DestDir: {app}\Lib\Stal\Eng
Source: BestCAD\Lib\Stal\Pol\*.*; DestDir: {app}\Lib\Stal\Pol
Source: BestCAD\Lib\Stal\Ger\*.*; DestDir: {app}\Lib\Stal\Ger
Source: BestCAD\Lib\Stal\*.*; DestDir: {app}\Lib\Stal
Source: BestCAD\Lib\Help\*; DestDir: {app}\Lib\Help; Flags: recursesubdirs
Source: BestCAD\Lib\ID16\*; DestDir: {app}\Lib\ID16; Flags: recursesubdirs
Source: BestCAD\Lib\ROS\*.*; DestDir: {app}\Lib\ROS
Source: BestCAD\Lib\RysParam\*.*; DestDir: {app}\Lib\RysParam; Flags: recursesubdirs
Source: BestCAD\Lib\Multiconsult\*.*; DestDir: {app}\Lib\Multiconsult; Flags: recursesubdirs
Source: BestCAD\Lib\*.*; DestDir: {app}\Lib
;; links from BestCAD\Most
Source: BestCAD\Most\Bariery\*.*; DestDir: {app}\Most\Bariery
Source: BestCAD\Most\Bariery\BarPor_h1_1\*.*; DestDir: {app}\Most\Bariery\BarPor_h1_1
Source: BestCAD\Most\Bariery\BarPor_h1_2\*.*; DestDir: {app}\Most\Bariery\BarPor_h1_2
Source: BestCAD\Most\Bariery\BarPor_h1_3\*.*; DestDir: {app}\Most\Bariery\BarPor_h1_3
Source: BestCAD\Most\Bariery\BarSP01\*.*; DestDir: {app}\Most\Bariery\BarSP01
Source: BestCAD\Most\Bariery\BarSP02\*.*; DestDir: {app}\Most\Bariery\BarSP02
Source: BestCAD\Most\Bariery\BarSP03\*.*; DestDir: {app}\Most\Bariery\BarSP03
Source: BestCAD\Most\Bariery\BarSP04\*.*; DestDir: {app}\Most\Bariery\BarSP04
Source: BestCAD\Most\Bariery\BarSP05\*.*; DestDir: {app}\Most\Bariery\BarSP05
Source: BestCAD\Most\Bariery\BarSP06\*.*; DestDir: {app}\Most\Bariery\BarSP06
Source: BestCAD\Most\Bariery\BarSP07\*.*; DestDir: {app}\Most\Bariery\BarSP07
Source: BestCAD\Most\Bariery\BarSP08\*.*; DestDir: {app}\Most\Bariery\BarSP08
Source: BestCAD\Most\Bariery\BarSP09\*.*; DestDir: {app}\Most\Bariery\BarSP09
Source: BestCAD\Most\Bariery\BarSP10\*.*; DestDir: {app}\Most\Bariery\BarSP10
Source: BestCAD\Most\Bariery\BarOrsta\*.*; DestDir: {app}\Most\Bariery\BarOrsta
Source: BestCAD\Most\BelkiPref\*.*; DestDir: {app}\Most\BelkiPref
Source: BestCAD\Most\Dylatacje\*.*; DestDir: {app}\Most\Dylatacje
Source: BestCAD\Most\EkranyAkust\*.*; DestDir: {app}\Most\EkranyAkust
Source: BestCAD\Most\Kolej\*.*; DestDir: {app}\Most\Kolej
Source: BestCAD\Most\Lozyska\*.*; DestDir: {app}\Most\Lozyska
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_J\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_J
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_S\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_S
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_W\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_C_W
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_EL\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_EL
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ELK\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ELK
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_J\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_J
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_S\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_S
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_W\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_ET_W
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_J\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_J
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_S\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_S
Source: BestCAD\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_W\*.*; DestDir: {app}\Most\Lozyska\Lozyska_ABF\£o¿_ABF_G_W
Source: BestCAD\Most\Media\*.*; DestDir: {app}\Most\Media
Source: BestCAD\Most\Odwodnienie\Inne\*.*; DestDir: {app}\Most\Odwodnienie\Inne
Source: BestCAD\Most\Odwodnienie\WavinHDPE\*.*; DestDir: {app}\Most\Odwodnienie\WavinHDPE
Source: BestCAD\Most\Oswietlenie\*.*; DestDir: {app}\Most\Oswietlenie
Source: BestCAD\Most\Prefabrykaty\Polbruk\*.*; DestDir: {app}\Most\Prefabrykaty\Polbruk
Source: BestCAD\Most\Prefabrykaty\Sytec\*.*; DestDir: {app}\Most\Prefabrykaty\Sytec
Source: BestCAD\Most\Prefabrykaty\Inne\*.*; DestDir: {app}\Most\Prefabrykaty\Inne
Source: BestCAD\Most\Schematy\PojaRzecz\*.*; DestDir: {app}\Most\Schematy\PojaRzecz
Source: BestCAD\Most\Schematy\PojaNorm\*.*; DestDir: {app}\Most\Schematy\PojaNorm
Source: BestCAD\Most\Schematy\SchemStat\*.*; DestDir: {app}\Most\Schematy\SchemStat
Source: BestCAD\Most\Schematy\PojaKat\*.*; DestDir: {app}\Most\Schematy\PojaKat
Source: BestCAD\Most\Skrajnie\*.*; DestDir: {app}\Most\Skrajnie
Source: BestCAD\Most\Sprezenie\*.*; DestDir: {app}\Most\Sprezenie
Source: BestCAD\Most\Technologia\MS54\*.*; DestDir: {app}\Most\Technologia\MS54
Source: BestCAD\Most\Technologia\Hunnebeck\dzwigary\*.*; DestDir: {app}\Most\Technologia\Hunnebeck\dzwigary
Source: BestCAD\Most\Technologia\Hunnebeck\klatki\*.*; DestDir: {app}\Most\Technologia\Hunnebeck\klatki
Source: BestCAD\Most\Technologia\Hunnebeck\deskowania\*.*; DestDir: {app}\Most\Technologia\Hunnebeck\deskowania
Source: BestCAD\Most\Technologia\*.*; DestDir: {app}\Most\Technologia
;; links from BestCAD\Text
Source: BestCAD\Text\*.*; DestDir: {app}\Text
Source: BestCAD\Text\Licencje2\*.*; DestDir: {app}\Licencje2
;; links from BestCAD\Help
Source: BestCAD\Help\*.*; DestDir: {app}\Help
;-----------------------------------------------------------
;; links for FullAC only
;-----------------------------------------------------------
;; links from Arx i Fas
Source: FullAC\NowyExe\x64\*.*; DestDir: {app}
Source: FullAC\*.*; DestDir: {app}
Source: FullAC\Arx\x64\*.*; DestDir: {app}\Arx
Source: FullAC\Fas\*.*; DestDir: {app}\Fas
Source: FullAC\Fas\par\*.*; DestDir: {app}\Fas\Par
Source: FullAC\Fas\ModelTerenu\*.*; DestDir: {app}\Fas\ModelTerenu
Source: FullAC\Fas\Niweleta\*.*; DestDir: {app}\Fas\Niweleta
Source: FullAC\Fas\odsR16\*.*; DestDir: {app}\Fas; Check: IsR16
Source: FullAC\Fas\odsR17\*.*; DestDir: {app}\Fas; Check: IsR17
;; links from OutPutAC
Source: OutPutAC\Licencja.txt; DestDir: {app}\Text
Source: OutPutAC\Zmiany.txt; DestDir: {app}\Text
Source: OutPutAC\Win7_Win8_Win10_Win11!.txt; DestDir: {app}\Text
Source: OutPutAC\Instalacja-Rejestracja BestCAD.pdf; DestDir: {app}\Pomoc
Source: OutPutAC\Readme_64bit.txt; DestDir: {app}\Text
Source: OutPutAC\BestCAD.ver; DestDir: {app}\Text
Source: OutPutAC\Dane.ser; DestDir: {app}\Text
;-----------------------------------------------------------
;; other links
;-----------------------------------------------------------
;; biblioteki from $Dll
Source: d:\3_Instalki\BestInstSetup\$Dll\mfc70.dll; DestDir: {sys}; Flags: onlyifdoesntexist uninsneveruninstall
Source: d:\3_Instalki\BestInstSetup\$Dll\mfc70.dll; DestDir: {syswow64}; Flags: onlyifdoesntexist uninsneveruninstall
Source: d:\3_Instalki\BestInstSetup\$Dll\msvcr70.dll; DestDir: {sys}; Flags: onlyifdoesntexist uninsneveruninstall
Source: d:\3_Instalki\BestInstSetup\$Dll\msvcr70.dll; DestDir: {syswow64}; Flags: onlyifdoesntexist uninsneveruninstall
;;te trzy przeniesiono do katalogu ARX
;Source: d:\3_Instalki\BestInstSetup\$Dll\x64\libcurld.dll; DestDir: {app}\Arx; Flags: uninsneveruninstall
;Source: d:\3_Instalki\BestInstSetup\$Dll\x64\pcreposix3.dll; DestDir: {app}\Arx; Flags: uninsneveruninstall
;Source: d:\3_Instalki\BestInstSetup\$Dll\x64\pcre3.dll; DestDir: {app}\Arx; Flags: uninsneveruninstall
;;
Source: d:\3_Instalki\BestInstSetup\$Dll\acdbENU.xmx; DestDir: {app}

[Icons]
Name: {group}\BestCAD 2026; Filename: {app}\BestCAD.exe; Parameters: /p BestCAD251 /t 2026Best.dwt /nologo; WorkingDir: {userdocs}; IconFilename: {app}\Best2026.ico; Comment: Uruchom BestCAD 2026
Name: {commondesktop}\BestCAD 2026; Filename: {app}\BestCAD.exe; Parameters: /p BestCAD251 /t 2026Best.dwt /nologo; WorkingDir: {userdocs}; IconFilename: {app}\Best2026.ico; Comment: Uruchom BestCAD 2026
Name: {group}\Uninstall BestCAD 2026; Filename: {uninstallexe}; IconFilename: {app}\Best2026.ico; IconIndex: 0; Comment: Odinstaluj BestCAD 2026

[_ISTool]
EnableISX=false
UseAbsolutePaths=false
OutputExeFilename=d:\3_Instalki\BestInstSetup\$BestLinki\251a\OutPutAC\BestCAD2026_64bit.exe

[Dirs]
Name: {app}\AplFiles
Name: {app}\Arx
Name: {app}\Fas
Name: {app}\Lib
Name: {app}\Most
Name: {app}\Text
Name: {app}\Pomoc


[LangOptions]
LanguageName=Polish
LanguageID=$0415

[Languages]
Name: default; MessagesFile: compiler:Polish.isl; LicenseFile: OutPutAC\Licencja.txt; InfoBeforeFile: OutPutAC\Readme_64bit.txt; InfoAfterFile: OutPutAC\Zmiany.txt

[Registry]

[UninstallDelete]
Name: {app}\Fas\*.*; Type: filesandordirs

[Run]
//Filename: {app}\Fas\RegistryPrepareTool.exe; Parameters: c:\BestCADPlikWymianyRej.txt; WorkingDir: {app}\Fas

Filename: "{app}\Fas\RegistryPrepareTool.exe"; \
  Parameters: """{code:GetExchangeFilePath}"""; \
  WorkingDir: "{app}\Fas"; \
  StatusMsg: "Uruchamianie programu przygotowuj¹cego rejestr..."

[Code]
const
  regAutoCAD = 'SOFTWARE\Autodesk\AutoCAD\';
  COMPANY_NAME = 'Ams';
  //plikWymianyRej = 'c:\BestCADPlikWymianyRej.txt';

//**************************************************************
//********** to trzeba zmieniæ przy zmianie wersji  ************
  BestCADProfVer = 'BestCAD251';
//**************************************************************

var
  tabAcadRegs       : TArrayOfString; //tablica ze stringami rejestrow Acadow w postaci 'R15.0-1:419'
  tabAcadNames      : TArrayOfString; //tablica ze stringami nazw Acadow w postaci 'AutoCAD 2012 (English)'
  tabProfilesAcad   : TArrayOfString; //tablica ze stringami Profili Acada
  ListBox           : TListBox;
  ListBoxProf       : TListBox;
  sSelectedAcad     : string;
  hWnd              : longint;
  sAcadRel          : string; //release Acada np. R15.0
  sAcadVer          : string; //wersja Acada np. 201-409
  regAcadProfiles   : string; //klucz do profili Acada
  sPathToBestCAD    : string; //sciezka do BestCADa
  sSelectProfil     : string;
  PageSelectAcad    : TWizardPage; //nowe okno wyboru Autocada
  PageSelectProf    : TWizardPage; //nowe okno wyboru profilu
  DefaultProfil     : TNewStaticText;
  TylkoJedenRaz     : Boolean; //sprawdza czy okno TWizardPage zainicjalizowane 1 raz
  ExchangeFilePath  : string;//zmienna ze sciezka zapisy pliku z danymi rejestrow do skopiowania

  
//////////////////////////////////////////////////////////////////////////////////////////////
//GetExchangeFilePath(Value: string): string; - zwraca sciezke do pliku wymiany wykorzystywanego w programie do kopiowania rejestrow
//////////////////////////////////////////////////////////////////////////////////////////////  
function GetExchangeFilePath(Value: string): string;
begin
  Result := ExchangeFilePath;
end;


//////////////////////////////////////////////////////////////////////////////////////////////
//UtworzRejestry (sAcadRelVer: string) - tworzy potrzebne rejestry
//////////////////////////////////////////////////////////////////////////////////////////////
procedure UtworzRejestry (sAcadRelVer: string);
var
  regAcadRelVer   : string; //klucz do Acada odpowiedniej wersji
  sAcadLocation   : string; //lokacja Acada
  sAktualProfil   : string; //aktualny profil Acada
  sZmiennaACAD    : string; //wartosc zmiennej ACAD support
  sDaneAplik      : string;
  sKey            : string; //
  i               : Integer;
  sProductName	  : string;
  sMsg1           : string;
  sMsg3           : string;
  sTemplatePath   : string;
  plikWymianyRej  : string;

begin
  // pobiera handle do okna
  hWnd := FindWindowByWindowName('Instalacja - BestCAD 2026');
  sPathToBestCAD := WizardDirValue;
  // dopisuje klucz BestCADa ze sciezka do BestCADa
  //HKEY_LOCAL_MACHINE\SOFTWARE\AMS\BestCAD181   BestCADLocation='C:\Program Files\BestCAD 2011'
  sKey := 'SOFTWARE\' + COMPANY_NAME + '\' + BestCADProfVer;
  RegWriteStringValue ( HKEY_LOCAL_MACHINE, sKey, 'BestCADLocation', sPathToBestCAD );
  //dopisuje klucz BestCADa z rejestrami AutoCADa
  //HKEY_LOCAL_MACHINE\SOFTWARE\AMS\BestCAD181   AcadRelease='R16.0'
  //HKEY_LOCAL_MACHINE\SOFTWARE\AMS\BestCAD181   AcadVersion='ACAD-201:415'
  sAcadRel := '';  sAcadVer := '';
  //for i:=1 to 5 do sAcadRel := sAcadRel + StrGet(sAcadRelVer, i);
  //for i:=7 to Length(sAcadRelVer) do sAcadVer := sAcadVer + StrGet(sAcadRelVer, i);
  
  
  sAcadRel := Copy(sAcadRelVer, 1, 5);  // Copy zaczyna od 1
  sAcadVer := Copy(sAcadRelVer, 7, Length(sAcadRelVer));
  
  
  
  RegWriteStringValue ( HKEY_LOCAL_MACHINE, sKey, 'AcadRelease', sAcadRel );
  RegWriteStringValue ( HKEY_LOCAL_MACHINE, sKey, 'AcadVersion', sAcadVer );
  //pobranie nazwy Acada
  RegQueryStringValue (HKEY_LOCAL_MACHINE, regAutoCAD + sAcadRelVer, 'ProductName', sProductName);
  sMsg1 := 'Instalator nie znalaz³ lokalizacji ' + sProductName + ' w rejestrach Twojego komputera.' + #13 + 'Przerwano dalsz¹ instalacjê. (1)';
  //sMsg2 := 'Instalator nie mo¿e odczytaæ aktualnego profilu ' + sProductName + ' .' + #13 + 'Przerwano dalsz¹ instalacjê. (2)';
  sMsg3 := sProductName + ' nie by³ jeszcze uruchamiany przez tego u¿ytkownika.' + #13 + 'Przerwano dalsz¹ instalacjê. (3)';
  // pobiera lokacje na dysku Acada
  regAcadRelVer := regAutoCAD + sAcadRel + '\' + sAcadVer;
  if ( not RegQueryStringValue ( HKEY_LOCAL_MACHINE, regAcadRelVer, 'AcadLocation', sAcadLocation ) ) then begin
    MsgBox(sMsg1, mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end;
  // teraz pobiera informacjê o aktualnym profilu
  //HKEY_CURRENT_USER\Software\Autodesk\AutoCAD\R16.0\ACAD-201:409\Profiles
  regAcadProfiles := regAcadRelVer + '\Profiles';
  {if ( not RegQueryStringValue ( HKEY_CURRENT_USER, regAcadProfiles, '', sAktualProfil ) ) then begin
    MsgBox( sMsg2, mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end;}
  sAktualProfil := sSelectProfil;
  //
  ExchangeFilePath := ExpandConstant('{tmp}') + '\BestCADPlikWymianyRej.txt';
  plikWymianyRej := ExchangeFilePath;
  //
  SaveStringToFile(plikWymianyRej, 'HKEY_CURRENT_USER\' + regAcadProfiles + '\' + sAktualProfil, True);
  //
  //teraz pobiera wartosc zmiennej ACAD z aktualnego profilu
  regAcadProfiles := regAcadProfiles + '\' + sAktualProfil + '\General';
  if ( not RegQueryStringValue ( HKEY_CURRENT_USER, regAcadProfiles, 'ACAD', sZmiennaACAD ) ) then begin
    MsgBox( sMsg3, mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end else begin
    //odczytanie sciezki do template z aktualnego profilu
	RegQueryStringValue ( HKEY_CURRENT_USER, regAcadProfiles, 'TemplatePath', sTemplatePath);
    //odczytanie po³o¿enia Danych Aplikacji w Document and Settings
    RegQueryStringValue ( HKEY_CURRENT_USER, 'Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders', 'AppData', sDaneAplik );
    // dopisanie œcie¿ki i wpisuje je do zmiennej ACAD nowego rejestru
    //HKEY_CURRENT_USER\Software\Autodesk\AutoCAD\R15.0\ACAD-1:415\Profiles\BestCAD181
    sZmiennaACAD := sDaneAplik + '\Ams\' + BestCADProfVer + ';' + sPathToBestCAD + ';' + sPathToBestCAD + '\Arx;' + sPathToBestCAD + '\Fas;' + sZmiennaACAD;
    regAcadProfiles := regAcadRelVer + '\Profiles' + '\' + BestCADProfVer + '\General';
    //
    SaveStringToFile(plikWymianyRej, '@' + 'HKEY_CURRENT_USER\' + regAcadRelVer + '\Profiles' + '\' + BestCADProfVer, True);
    //
    //zapisanie danych o rejestrze do pliku tekstowego
    //plik ten wykorzystuje program RegistryPrepareTool.exe
    SaveStringToFile(plikWymianyRej, '@' + 'HKEY_CURRENT_USER\' + regAcadProfiles, True);
    SaveStringToFile(plikWymianyRej, '@' + 'ACAD', True);
    SaveStringToFile(plikWymianyRej, '@' + sZmiennaACAD, True);
    //dopisanie do pliku równiez template
    SaveStringToFile(plikWymianyRej, '@' + 'HKEY_CURRENT_USER\' + regAcadProfiles, True);
    SaveStringToFile(plikWymianyRej, '@' + 'QnewTemplate', True);
    SaveStringToFile(plikWymianyRej, '@' + sTemplatePath + '\2026Best.dwt', True);
    //
    //RegWriteStringValue ( HKEY_CURRENT_USER, regAcadProfiles, 'ACAD', sZmiennaACAD );
  end;
end; { UtworzRejestry }

function ExpandUserEnvVars(S: string): string;
var
  UserProfile: string;
begin
  UserProfile := GetEnv('UserProfile'); // np. C:\Users\Jan
  StringChange(S, '%UserProfile%', UserProfile);
  Result := S;
end;


//////////////////////////////////////////////////////////////////////////////////////////////
//KopiujSzablony ()
//////////////////////////////////////////////////////////////////////////////////////////////
procedure KopiujSzablony ();
var
  sTemplatePath   : string; //sciezka do katalogu Template
  sAktualProfil	  : string;
  regAcadProfiles : string;
begin
  regAcadProfiles := regAutoCAD + sAcadRel + '\' + sAcadVer + '\Profiles';
  RegQueryStringValue ( HKEY_CURRENT_USER, regAcadProfiles, '', sAktualProfil );
  regAcadProfiles := regAcadProfiles + '\' + sAktualProfil + '\General';
  // teraz pobiera lokacje TemplatePath
  RegQueryStringValue ( HKEY_CURRENT_USER, regAcadProfiles, 'TemplatePath', sTemplatePath );
  sTemplatePath := ExpandUserEnvVars(sTemplatePath);
  //ten warunek zosta³ dodany poniewa¿ w Civil 3D katalog ten nie istnia³
  //i podczas uruchamiania programu pojawia³ siê b³¹d kopiowania template 3
  if not DirExists(sTemplatePath) then begin
	CreateDir(sTemplatePath);
  end;
  //w zaleznosci od wersji Acada sAcadRel
  if sAcadRel = 'R15.0' then begin
    //skopiowanie szablonu do Template
    FileCopy(sPathToBestCAD + '\2026Best.dwt', sTemplatePath + '\2026Best.dwt', False);
  end else begin
    FileCopy(sPathToBestCAD + '\2026Best.dwt', sTemplatePath + '\2026Best.dwt', False);
    //dodanie do rejestrow informacji o QNEW - przenios³em do UtworzRejestry
  	//sTemplatePath := sTemplatePath + '\2011Best.dwt';
  	//regAcadProfiles := regAutoCAD + sAcadRel + '\' + sAcadVer + '\Profiles\' + BestCADProfVer + '\General';
    //RegWriteStringValue ( HKEY_CURRENT_USER, regAcadProfiles, 'QnewTemplate', sTemplatePath )
  end
end;



//Sprawdzam czy kopiowaæ plik CIUX (Od Autocad 2010 [18.0])
function IsCUIX: Boolean;
begin
  if (sAcadRel = 'R18.0') or (sAcadRel = 'R18.1') or (sAcadRel = 'R18.2') or (sAcadRel = 'R19.0') or (sAcadRel = 'R19.1') or (sAcadRel = 'R20.0') or (sAcadRel = 'R20.1') or (sAcadRel = 'R21.0')or (sAcadRel = 'R22.0') or (sAcadRel = 'R23.0') or (sAcadRel = 'R23.1') or (sAcadRel = 'R24.0') or (sAcadRel = 'R24.1') or (sAcadRel = 'R24.2') or (sAcadRel = 'R24.3') or (sAcadRel = 'R25.0') or (sAcadRel = 'R25.1') then begin
    Result := TRUE;
  end else begin
    Result := False;
  end;
end;



//Sprawdzam czy kopiowaæ plik mns (Do Autocad w³¹cznie [17.2])
function IsMns: Boolean;
begin
  if (sAcadRel <> 'R18.0') and (sAcadRel <> 'R18.1') and (sAcadRel <> 'R18.2') and (sAcadRel <> 'R19.0') and (sAcadRel <> 'R19.1') and (sAcadRel <> 'R20.0') and (sAcadRel <> 'R20.1') and (sAcadRel <> 'R21.0') and (sAcadRel <> 'R22.0') and (sAcadRel <> 'R23.0') and (sAcadRel <> 'R23.1')and (sAcadRel <> 'R24.0')and (sAcadRel <> 'R24.1') and (sAcadRel <> 'R24.2') and (sAcadRel <> 'R24.3') and (sAcadRel <> 'R25.0') and (sAcadRel = 'R25.1') then begin
    Result := TRUE;
  end else begin
    Result := False;
  end;
end;



function IsR16: Boolean;
begin
  if (sAcadRel <> 'R17.0') and (sAcadRel <> 'R17.1') and (sAcadRel <> 'R17.2') and (sAcadRel <> 'R18.0') and (sAcadRel <> 'R18.1') and (sAcadRel <> 'R18.2') and (sAcadRel <> 'R19.0') and (sAcadRel <> 'R19.1') and (sAcadRel <> 'R20.0')and (sAcadRel <> 'R20.1') and (sAcadRel <> 'R21.0') and (sAcadRel <> 'R22.0') and (sAcadRel <> 'R23.0') and (sAcadRel <> 'R23.1') and (sAcadRel <> 'R24.0') and (sAcadRel <> 'R24.1')and (sAcadRel <> 'R24.2') and (sAcadRel <> 'R24.3') and (sAcadRel <> 'R25.0') and (sAcadRel = 'R25.1') then begin
    Result := TRUE;
  end else begin
    Result := False;
  end;
end;



function IsR17: Boolean;
begin
  if (sAcadRel = 'R17.0') or (sAcadRel = 'R17.1') or (sAcadRel = 'R17.2') or (sAcadRel = 'R18.0') or (sAcadRel = 'R18.1') or (sAcadRel = 'R18.2') or (sAcadRel = 'R19.0') or (sAcadRel = 'R19.1') or (sAcadRel = 'R20.0') or (sAcadRel = 'R20.1')or (sAcadRel = 'R21.0')or (sAcadRel = 'R22.0')or (sAcadRel = 'R23.0') or (sAcadRel = 'R23.1') or (sAcadRel = 'R24.0')or (sAcadRel = 'R24.1')or (sAcadRel = 'R24.2') or (sAcadRel = 'R24.3') or (sAcadRel = 'R25.0') or (sAcadRel = 'R25.1') then begin
    Result := TRUE;
  end else begin
    Result := False;
  end;
end;



procedure ZmienNazweFolderuWDocumentsAndSettings ();
var
	sDaneAplik, sNowaNazwaFolderu : string;
	czysukces: Boolean;
begin
  //odczytanie po³o¿enia Danych Aplikacji w Document and Settings
  RegQueryStringValue ( HKEY_CURRENT_USER, 'Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders', 'AppData', sDaneAplik );
	//sciezka do folderu o nazwie jak profil w Dane Aplikacji\Ams\
  sDaneAplik := sDaneAplik + '\Ams\' + BestCADProfVer;
  //sprawdzamy czy folder o takiej nawie istnieje jezeli tak to zmieniamy jego nazwe dodajac na koncu znak wykrzyknika
  //trzeba sprawdzic czy nie ma folderów z dodanymi juz wykrzyknikami jezeli tak to dojajemy o jeden wiecej
  if DirExists(sDaneAplik) then begin
    sNowaNazwaFolderu := sDaneAplik
    while DirExists(sNowaNazwaFolderu) do begin
      sNowaNazwaFolderu := sNowaNazwaFolderu + '!';
    end;
    czysukces := RenameFile(sDaneAplik, sNowaNazwaFolderu);

  end;
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//DodajZaufaneKatalogi ()
//////////////////////////////////////////////////////////////////////////////////////////////
procedure DodajZaufaneKatalogi ();
var
  NamesKeyUsers: TArrayOfString;
  TrustedValue, regAcadTrustedKey: String;
begin
  if(sAcadRel = 'R19.1') or (sAcadRel = 'R20.0') or (sAcadRel = 'R20.1') or (sAcadRel = 'R21.0') or (sAcadRel = 'R22.0') or (sAcadRel = 'R23.0') or (sAcadRel = 'R23.1')or (sAcadRel = 'R24.0')or (sAcadRel = 'R24.1')or (sAcadRel = 'R24.2') or (sAcadRel = 'R24.3') or (sAcadRel = 'R25.0') then begin
    regAcadTrustedKey := regAutoCAD + sAcadRel + '\' + sAcadVer + '\Profiles\' + AnsiUppercase(BestCADProfVer) + '\Variables';
    if RegQueryStringValue(HKEY_CURRENT_USER, regAcadTrustedKey, 'TRUSTEDPATHS', TrustedValue) then begin
      if ((Length(TrustedValue) > 0) and (TrustedValue[Length(TrustedValue)] = ';')) or (Length(TrustedValue) = 0) then begin
        TrustedValue := TrustedValue + sPathToBestCAD + ';' + sPathToBestCAD + '\Arx;' + sPathToBestCAD + '\Fas';
      end else begin
        TrustedValue := TrustedValue + ';' + sPathToBestCAD + ';' + sPathToBestCAD + '\Arx;' + sPathToBestCAD + '\Fas';
      end;
      RegWriteStringValue(HKEY_CURRENT_USER, regAcadTrustedKey, 'TRUSTEDPATHS', ExpandConstant(TrustedValue));
    end else begin
      TrustedValue := sPathToBestCAD + ';' + sPathToBestCAD + '\Arx;' + sPathToBestCAD + '\Fas';
      RegWriteStringValue(HKEY_CURRENT_USER, regAcadTrustedKey, 'TRUSTEDPATHS', ExpandConstant(TrustedValue));
    end;
  end;
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//KopiujFonty ()
//////////////////////////////////////////////////////////////////////////////////////////////
procedure KopiujFonty ();
var
  sFontsPath   : string; //sciezka do katalogu Fonts
  sAcadLocation : string;
  regAcadRelVer : string;
begin
  regAcadRelVer := regAutoCAD + sAcadRel + '\' + sAcadVer;
  RegQueryStringValue (HKEY_LOCAL_MACHINE, regAcadRelVer, 'AcadLocation', sAcadLocation);
  sFontsPath := sAcadLocation + '\Fonts';
  //skopiowanie czcionek do Fonts
  FileCopy(sPathToBestCAD + '\cdmbigpl.shx', sFontsPath + '\cdmbigpl.shx', False);
  FileCopy(sPathToBestCAD + '\cdmsimpl.shx', sFontsPath + '\cdmsimpl.shx', False);
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//KopiujacdbENU ()
//////////////////////////////////////////////////////////////////////////////////////////////
procedure KopiujacdbENU ();
var
  sLanguage   : string; //sciezka do katalogu Fonts
  regAcadRelVer : string;
  sAcadLocation : string;
begin
  regAcadRelVer := regAutoCAD + sAcadRel + '\' + sAcadVer;
  RegQueryStringValue (HKEY_LOCAL_MACHINE, regAcadRelVer, 'AcadLocation', sAcadLocation);
  RegQueryStringValue (HKEY_LOCAL_MACHINE, regAcadRelVer, 'Language', sLanguage);
  if (sAcadRel = 'R15.0') and (sLanguage = 'Polish') then begin
	  //skopiowanie acdbENU.xmx do Acada i usuniecie pliku jesli wersja nie jest angielska
	  FileCopy(sPathToBestCAD + '\acdbENU.xmx', sAcadLocation + '\acdbENU.xmx', False);
	  DeleteFile(sPathToBestCAD + '\acdbENU.xmx');
  end;
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//GetRegAcads () - zwraca rejestry Acadow
//////////////////////////////////////////////////////////////////////////////////////////////
procedure GetRegAcads;
var
  strSubKeysAcad: string;
  sProductName, sLanguage: string;
  i, j, n, nAcadRegs, nMainRegs, nNewRegs, nNewRegsCorrect: Integer;
  tabAcadMainRegs: TArrayOfString;    //wszystkie wersje Autocadów  z LOCAL_MACHINA w postaci "R19.0"
  tabAcadNewMainRegs: TArrayOfString;   //wersje Autocadów z LOCAL_MACHINE które zezwalamy instalowaæ
  tabNewRegs: TArrayOfString;   //podwersje Autocadów w postaci 201:419 (oznaczaj¹ce jaki Autocad jest zainstalowany np Civil, Architectural itp)
begin
  //wyciagniecie rejestrow na poziomie R15.0;
  RegGetSubkeyNames(HKEY_LOCAL_MACHINE, regAutoCAD, tabAcadMainRegs);
  //wyrzucenie z tablicy Acadow innych niz 17.2, 18.0, 18.1
  nMainRegs:=GetArrayLength(tabAcadMainRegs);
  j:=0;
  for i:=0 to (nMainRegs-1) do begin
    if (tabAcadMainRegs[i] = 'R17.1') or (tabAcadMainRegs[i] = 'R17.2') or (tabAcadMainRegs[i] = 'R18.0') or (tabAcadMainRegs[i] = 'R18.1') or (tabAcadMainRegs[i] = 'R18.2') or (tabAcadMainRegs[i] = 'R19.0') or (tabAcadMainRegs[i] = 'R19.1') or (tabAcadMainRegs[i] = 'R20.0') or (tabAcadMainRegs[i] = 'R20.1') or (tabAcadMainRegs[i] = 'R21.0') or (tabAcadMainRegs[i] = 'R22.0') or (tabAcadMainRegs[i] = 'R23.0') or (tabAcadMainRegs[i] = 'R23.1')or (tabAcadMainRegs[i] = 'R24.0')or (tabAcadMainRegs[i] = 'R24.1')or (tabAcadMainRegs[i] = 'R24.2') or (tabAcadMainRegs[i] = 'R24.3') or (tabAcadMainRegs[i] = 'R25.0') or (tabAcadMainRegs[i] = 'R25.1') then begin
      j:=j+1;
      SetArrayLength(tabAcadNewMainRegs, j);
      tabAcadNewMainRegs[j-1]:=tabAcadMainRegs[i];
    end;
  end;
  //wyciagniecie rejestrow na poziomie 201:419
  nMainRegs:=GetArrayLength(tabAcadNewMainRegs);
  nAcadRegs:=0; n:=0;
  for i:=0 to (nMainRegs-1) do begin
    strSubKeysAcad:=regAutoCAD + tabAcadNewMainRegs[i];
    RegGetSubkeyNames(HKEY_LOCAL_MACHINE, strSubKeysAcad, tabNewRegs);
    nNewRegs:=GetArrayLength(tabNewRegs);
    {
    nAcadRegs:=nAcadRegs+nNewRegs;
    SetArrayLength(tabAcadRegs, nAcadRegs);
    SetArrayLength(tabAcadNames, nAcadRegs);
    for j:=0 to (nNewRegs-1) do begin
      tabAcadRegs[j+n]:=tabAcadNewMainRegs[i] + '\' + tabNewRegs[j];
      RegQueryStringValue (HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadRegs[j+n], 'ProductName', sProductName);
      RegQueryStringValue (HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadRegs[j+n], 'Language', sLanguage);
      tabAcadNames[j+n]:=sProductName + '  (' + sLanguage + ')';
    end;
    }
    //Sprawdzam ile kluczy Autocada jest poprawnych (czyli takich kluczy w postaci 201:419 które zawieraj¹ watoœci ProductName i Language)
    nNewRegsCorrect:=0;
    for j:=0 to (nNewRegs-1) do begin
      if RegValueExists(HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadNewMainRegs[i] + '\' + tabNewRegs[j], 'ProductName') and RegValueExists(HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadNewMainRegs[i] + '\' + tabNewRegs[j], 'Language') then
      begin
        nNewRegsCorrect:=nNewRegsCorrect + 1;
      end;
    end;

    //Dodaje poprawn¹ liczbê Autocadów (poprawnych kluczy) do globalnych tablic zawieraj¹cych nazwê autocada oraz sciê¿kê do jego wpisów w rejestrach
    nAcadRegs:=nAcadRegs+nNewRegsCorrect;
    SetArrayLength(tabAcadRegs, nAcadRegs);
    SetArrayLength(tabAcadNames, nAcadRegs);


    nNewRegsCorrect:=0
    for j:=0 to (nNewRegs-1) do begin
      if RegQueryStringValue (HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadNewMainRegs[i] + '\' + tabNewRegs[j], 'ProductName', sProductName) and RegQueryStringValue (HKEY_LOCAL_MACHINE, regAutoCAD + tabAcadNewMainRegs[i] + '\' + tabNewRegs[j], 'Language', sLanguage) then
      begin
        tabAcadRegs[nNewRegsCorrect+n]:=tabAcadNewMainRegs[i] + '\' + tabNewRegs[j];
        tabAcadNames[nNewRegsCorrect+n]:=sProductName + '  (' + sLanguage + ')';
        nNewRegsCorrect:=nNewRegsCorrect + 1;
      end;
    end;
    
    n:=nAcadRegs;
  end;
end; { GetRegAcads }



function CzyNieZawieraBest(NazwaProfilu: string): Boolean;
var
  wynik: Boolean;
  WersjaProf: string;
  IntNumerWersji: Integer;
begin
  wynik := True;
  IntNumerWersji := 0;
  NazwaProfilu := AnsiUppercase(NazwaProfilu);
  WersjaProf := Copy(NazwaProfilu, 8, 3);
  IntNumerWersji := StrToIntDef(WersjaProf, 0);
  if ( ( Pos('BESTCAD', NazwaProfilu) > 0 ) and (IntNumerWersji <> 0) )then
  begin
    wynik := False;
  end;
  Result := wynik;
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//GetRegProfilesAcad(SelAcad: string)
//pobiera z rejestrow profile wybranego Autocada
//////////////////////////////////////////////////////////////////////////////////////////////
function GetRegProfilesAcad(SelAcad: string): TArrayOfString;
var
  i, k: Integer;
  PathToAcad, Blad, Blad1: string;
  AllProfilesAcad: TArrayOfString;
  ProfileBezBest: TArrayOfString;
begin
  PathToAcad := regAutoCAD + SelAcad + '\' + 'Profiles';
  Blad := 'Instalator nie mo¿e odczytaæ profili ' + sSelectedAcad + ' .' + #13 + 'Przerwano dalsz¹ instalacjê. (2)';

  if ( not RegGetSubkeyNames(HKEY_CURRENT_USER, PathToAcad, AllProfilesAcad) ) then
  begin
    MsgBox( Blad, mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end;

  k := 0;
  for i:=0 to ( GetArrayLength(AllProfilesAcad) - 1 ) do
  begin
    if (Length(AllProfilesAcad[i]) = 10) then
    begin
      if (CzyNieZawieraBest(AllProfilesAcad[i])) then
      begin
        k := k + 1;
      end;
    end else
    begin
      k := k + 1;
    end;
  end;

  if k = 0 then
  begin
    Blad1 := 'Brak profili lub utwórz inny profil niz BESTCAD';
    MsgBox( Blad1, mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end;
  SetArrayLength(ProfileBezBest, k);
  k := 0;
  for i:=0 to ( GetArrayLength(AllProfilesAcad) - 1 ) do
  begin
    {if  ( (Length(AllProfilesAcad[i]) <> 10) and (CzyNieZawieraBest(AllProfilesAcad[i])) ) then
    begin
      ProfileBezBest[k] := AllProfilesAcad[i];
      k := k + 1;
    end;}
    if (Length(AllProfilesAcad[i]) = 10) then
    begin
      if (CzyNieZawieraBest(AllProfilesAcad[i])) then
      begin
        ProfileBezBest[k] := AllProfilesAcad[i];
        k := k + 1;
      end;
    end else
    begin
      ProfileBezBest[k] := AllProfilesAcad[i];
      k := k + 1;
    end;
  end;

  Result := ProfileBezBest;
end; { GetRegProfilesAcad }



//////////////////////////////////////////////////////////////////////////////////////////////
//ListBoxProfOnClick(Sender: TObject) - obs³uga zdarzenia wyboru profilu Acada
//////////////////////////////////////////////////////////////////////////////////////////////
procedure ListBoxProfOnClick(Sender: TObject);
var Index : integer;
begin
  Index := ListBoxProf.ItemIndex;
  sSelectProfil := tabProfilesAcad[Index];
end; { ListBoxProfOnClick }



//////////////////////////////////////////////////////////////////////////////////////////////
//OknoProfiluAcadaa() - wyswietla i wypelnia okno z profilami Acada
//////////////////////////////////////////////////////////////////////////////////////////////
procedure OknoProfiluAcada;//(var Next: Boolean);
var
  i, n, iProfilDefault: Integer;
begin

  ListBoxProf := TListBox.Create(PageSelectProf);
  ListBoxProf.Width := PageSelectProf.SurfaceWidth;
  ListBoxProf.Parent := PageSelectProf.Surface;
  ListBoxProf.OnClick := @ListBoxProfOnClick;
  n := GetArrayLength(tabProfilesAcad) - 1;

  //for i:=0 to n do ListBoxProf.Items.Add(tabProfilesAcad[i]);
  //ListBoxProf.ItemIndex := 0;


  iProfilDefault:=0;
  for i:=0 to n do 
    begin
      ListBoxProf.Items.Add(tabProfilesAcad[i]);
      if (tabProfilesAcad[i] = '<<Profil bez nazwy>>') then begin
        iProfilDefault:=i;
      end
    end;

  ListBoxProf.ItemIndex := iProfilDefault;
  
  DefaultProfil := TNewStaticText.Create(PageSelectProf);
  DefaultProfil.Top := ListBoxProf.Top + ListBoxProf.Height + ScaleY(8);
  DefaultProfil.Caption := 'Domyœlnym profilem:' #13 '  -dla AutoCAD jest <<Profil bez nazwy>>' #13 '  -dla AutoCAD Civil jest <<AUTOCAD>>';
  DefaultProfil.AutoSize := True;
  DefaultProfil.Parent := PageSelectProf.Surface;
end; { OknoProfiluAcada }



//////////////////////////////////////////////////////////////////////////////////////////////
//ListBoxOnClick(Sender: TObject) - obs³uga zdarzenia wyboru wersji Acada
//////////////////////////////////////////////////////////////////////////////////////////////
procedure ListBoxOnClick(Sender: TObject);
var Index : integer;
begin
  Index := ListBox.ItemIndex;
  sSelectedAcad := tabAcadRegs[Index];
  tabProfilesAcad := GetRegProfilesAcad(sSelectedAcad);
  OknoProfiluAcada();
  sSelectProfil := tabProfilesAcad[0];
end; { ListBoxOnClick }



//////////////////////////////////////////////////////////////////////////////////////////////
//OknoWersjiAcada() - wyswietla i wypelnia okno z wersjami Acada
//////////////////////////////////////////////////////////////////////////////////////////////
procedure OknoWersjiAcada;//(var Next: Boolean);
var
  i, n: Integer;
begin

  ListBox := TListBox.Create(PageSelectAcad);
  ListBox.Width := PageSelectAcad.SurfaceWidth;
  ListBox.Parent := PageSelectAcad.Surface;
  ListBox.OnClick := @ListBoxOnClick;
  n := GetArrayLength(tabAcadNames) - 1;

  for i:=0 to n do ListBox.Items.Add(tabAcadNames[i]);
  ListBox.ItemIndex := 0;
end;



//////////////////////////////////////////////////////////////////////////////////////////////
//ScriptDlgPages(CurPage: Integer; BackClicked: Boolean): Boolean;
//akcja w zale¿noœci od biezacej strony instalatora
//////////////////////////////////////////////////////////////////////////////////////////////
function ScriptDlgPages (CurPage: Integer; BackClicked: Boolean): Boolean;
var
  nAcadRegs: Integer;
begin
  Result := True;
  //pomiedzy wpInfoBefore a wpSelectDir sprawdzic jakie sa Acady i zapytac o wersje uzytkownika
  if (not BackClicked and (CurPage = wpInfoBefore)) then begin
    //wyszukujanie zainstalowanyc Acadow
    GetRegAcads;
    nAcadRegs:=GetArrayLength(tabAcadRegs);
    case nAcadRegs of
      0:
        begin
          //jesli nie ma zadnego Acada przerywamy instalacje
          MsgBox('Instalator nie wykry³ w rejestrach tego komputera ¿adnej wersji' #13 'AutoCADa 2008,2009,2010,2011,2012,2013,2014,2015,2016,2017,2018,2019,2020,2021,2022,2023,2024,2025,2026' #13 'Przerwano dalsz¹ instalacjê.', mbError, MB_OK );
          SendMessage(hWnd, $010, $00000000 , $00000000);
        end;
      1:
        begin
           //jesli jest tylko jeden Acad, to nie trzeba wywolywac dodatkowego okna z wyborem wersji
          sSelectedAcad:=tabAcadRegs[0];
          if TylkoJedenRaz then begin
            //Nowe okno wyboru profilu na ktorym autocad zostanie zainstalowany
            PageSelectProf := CreateCustomPage(wpInfoBefore, 'Wybór profilu AutoCADa','Zainstalowany AutoCAD posiada nastepujace profile' #13 'Wybierz jeden na którym bêdzie zainstalowany Bestcad:');
            TylkoJedenRaz := False;
          end;
          tabProfilesAcad := GetRegProfilesAcad(sSelectedAcad);
          OknoProfiluAcada();
          sSelectProfil := tabProfilesAcad[0];
        end;
      else
        begin
          //jesli jest wiecej Acadow to trzeba wywolac okno z wyborem wersji
          sSelectedAcad:=tabAcadRegs[0];

          //Okno moze byc tworzone tylko jeden raz, jesli nie bedzie tego warunku w przpadku gdy ktos nacisnie wstecz i program ponownie przejdzie przez
          //ta petle okna wyboru Autocada i wyboru profilu beda wyswietlac sie n razy gdzie n ilosc przejsc przez petle
          if TylkoJedenRaz then begin
            //Nowe okno wyboru Autocada, tylko w przypadku gdy ilosc Autocadow > 1
            PageSelectAcad := CreateCustomPage(wpInfoBefore, 'Wybór wersji AutoCADa', 'Na Twoim komputerze zainstalowane s¹ poni¿sze wersje AutoCADa.' #13 'Wybierz jedn¹ z nich.');
            //Nowe okno wyboru profilu na ktorym autocad zostanie zainstalowany
            PageSelectProf := CreateCustomPage(PageSelectAcad.ID, 'Wybór profilu AutoCADa','Wybrany AutoCAD posiada nastêpuj¹ce profile' #13 'Wybierz jeden na którym bêdzie zainstalowany Bestcad:');
            TylkoJedenRaz := False;
          end;

          OknoWersjiAcada();
          tabProfilesAcad := GetRegProfilesAcad(sSelectedAcad);
          OknoProfiluAcada();
          sSelectProfil := tabProfilesAcad[0];
        end;
    end;
  end;
    //przed koncem, ale po zinstalowaniu plikow trzeba skopiowac plik szablonu
    if CurPage = wpInfoAfter then
      if not BackClicked then begin
      	KopiujSzablony;
      	KopiujFonty;
      	KopiujacdbENU;
        ZmienNazweFolderuWDocumentsAndSettings;
        DodajZaufaneKatalogi;
      end;
end; { ScriptDlgPages }



//////////////////////////////////////////////////////////////////////////////////////////////
//InitializeSetup() - funkcja wywo³ywana jeszcze przed procedur¹ InitializeWizard
//////////////////////////////////////////////////////////////////////////////////////////////
function InitializeSetup(): Boolean;
var
  nAcadRegs : Integer;
begin
  GetRegAcads;
  nAcadRegs:=GetArrayLength(tabAcadRegs);
  if  nAcadRegs=0 then begin
     //hWnd := FindWindowByWindowName('Instalacja - SyteCAD 2025');
     MsgBox('Instalator nie wykry³ w rejestrach tego komputera ¿adnej wersji' #13 'AutoCADa 2008,2009,2010,2011,2012,2013,2014,2015,2016,2017,2018,2019,2020,2021,2022,2023,2024,2025,2026' #13 'Przerwano dalsz¹ instalacjê.', mbError, MB_OK );
     //SendMessage(hWnd, $010, $00000000 , $00000000);
     Result := false;
  end else begin
     Result := true;
  end;
end; { InitializeSetup }



//////////////////////////////////////////////////////////////////////////////////////////////
//InitializeWizard() - inicjuje calego Wizarda
//////////////////////////////////////////////////////////////////////////////////////////////
procedure InitializeWizard();
var
  CancelButton: TButton;
begin
  hWnd := FindWindowByWindowName('Instalacja - BestCAD 2026');
  //sprawdzenie czy uzytkownik ma prawa administratora
  if not IsAdminLoggedOn then begin
    //jesli nie to zakoñczenie instalacji
    MsgBox('Instalacja mo¿e byæ wykonana tylko przez Administratora !' #13 'Przerwano dalsz¹ instalacjê.', mbError, MB_OK );
    SendMessage(hWnd, $010, $00000000 , $00000000);
  end else
  CancelButton := WizardForm.CancelButton;

  //sprawdzenie wersji windows i jesli nie jest to Win2k/XP
  //hWnd := InstallOnThisVersion('4.9', '4.1');
  TylkoJedenRaz := True;
end; { InitializeWizard }



//////////////////////////////////////////////////////////////////////////////////////////////
//NextButtonClick(CurPage: Integer): Boolean; - obsluga zdarzenia NextButtonClick
//BackButtonClick(CurPage: Integer): Boolean; - obsluga zdarzenia BackButtonClick
//////////////////////////////////////////////////////////////////////////////////////////////
function NextButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, False);
  if CurPage=wpReady then begin
      UtworzRejestry (sSelectedAcad);
  end;
end; { NextButtonClick }
//
function BackButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, True);
end; { BackButtonClick }
